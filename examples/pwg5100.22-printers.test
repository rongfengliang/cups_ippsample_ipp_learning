#
# Printer tests for conformance with PWG 5100.22-2019
#
# Usage:
#
#   ./ipptool -tIf FILENAME system-uri pwg5100.22-printer.test
#
# Copyright © 2022 by the Printer Working Group.
#
# Licensed under Apache License v2.0.  See the file "LICENSE" for more
# information.
#

{
    NAME "Disable-All-Printers"
    OPERATION Disable-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Enable-All-Printers"
    OPERATION Enable-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Pause-All-Printers-After-Current-Job"
    OPERATION Pause-All-Printers-After-Current-Job
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Pause-All-Printers"
    OPERATION Pause-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Resume-All-Printers"
    OPERATION Resume-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Shutdown-All-Printers"
    OPERATION Shutdown-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Startup-All-Printers"
    OPERATION Startup-All-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Restart-System"
    OPERATION Restart-System
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok
}

{
    NAME "Get-Printer-Attributes"
    OPERATION Get-Printer-Attributes
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok

    EXPECT printer-id IN-GROUP printer-attributes-tag OF-TYPE integer(1:65535)|no-value COUNT 1
    EXPECT printer-id DEFINE-VALUE DEFAULT_PRINTER_ID
    EXPECT printer-info IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-is-accepting-jobs IN-GROUP printer-attributes-tag OF-TYPE boolean COUNT 1
    EXPECT printer-location IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-make-and-model IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-more-info IN-GROUP printer-attributes-tag OF-TYPE uri COUNT 1
    EXPECT printer-name IN-GROUP printer-attributes-tag OF-TYPE name(127) COUNT 1
    EXPECT printer-state IN-GROUP printer-attributes-tag OF-TYPE enum COUNT 1
    EXPECT printer-state-reasons IN-GROUP printer-attributes-tag OF-TYPE keyword
    EXPECT printer-uri-supported IN-GROUP printer-attributes-tag OF-TYPE uri
    EXPECT printer-xri-supported IN-GROUP printer-attributes-tag OF-TYPE collection SAME-COUNT-AS printer-uri-supported
    EXPECT uri-authentication-supported IN-GROUP printer-attributes-tag OF-TYPE keyword SAME-COUNT-AS printer-uri-supported
    EXPECT uri-security-supported IN-GROUP printer-attributes-tag OF-TYPE keyword SAME-COUNT-AS printer-uri-supported
}

{
    NAME "Shutdown-One-Printer"
    OPERATION Shutdown-One-Printer
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR integer printer-id $DEFAULT_PRINTER_ID

    STATUS successful-ok
}

{
    NAME "Startup-One-Printer"
    OPERATION Startup-One-Printer
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR integer printer-id $DEFAULT_PRINTER_ID

    STATUS successful-ok
}

{
    NAME "Restart-One-Printer"
    OPERATION Restart-One-Printer
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR integer printer-id $DEFAULT_PRINTER_ID

    STATUS successful-ok
}

{
    NAME "Get-Printers"
    OPERATION Get-Printers
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri

    STATUS successful-ok

    EXPECT printer-id IN-GROUP printer-attributes-tag OF-TYPE integer(1:65535)|no-value COUNT 1
    EXPECT printer-info IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-is-accepting-jobs IN-GROUP printer-attributes-tag OF-TYPE boolean COUNT 1
    EXPECT printer-location IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-make-and-model IN-GROUP printer-attributes-tag OF-TYPE text(127) COUNT 1
    EXPECT printer-more-info IN-GROUP printer-attributes-tag OF-TYPE uri COUNT 1
    EXPECT printer-name IN-GROUP printer-attributes-tag OF-TYPE name(127) COUNT 1
    EXPECT printer-state IN-GROUP printer-attributes-tag OF-TYPE enum COUNT 1
    EXPECT printer-state-reasons IN-GROUP printer-attributes-tag OF-TYPE keyword
    EXPECT printer-uri-supported IN-GROUP printer-attributes-tag OF-TYPE uri
    EXPECT printer-xri-supported IN-GROUP printer-attributes-tag OF-TYPE collection SAME-COUNT-AS printer-uri-supported
    EXPECT uri-authentication-supported IN-GROUP printer-attributes-tag OF-TYPE keyword SAME-COUNT-AS printer-uri-supported
    EXPECT uri-security-supported IN-GROUP printer-attributes-tag OF-TYPE keyword SAME-COUNT-AS printer-uri-supported
}

{
    NAME "Create-Printer (Test Printer)"
    OPERATION Create-Printer
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR keyword printer-service-type print
    GROUP printer-attributes-tag
	ATTR name printer-name "Test Printer"
	ATTR text printer-info "5100.22 Test Printer"
	ATTR text printer-location "Test Location"
	ATTR uri printer-geo-location "geo:46.4707,-80.9961"


    STATUS successful-ok
    EXPECT printer-id IN-GROUP printer-attributes-tag OF-TYPE integer(1:MAX) COUNT 1
    EXPECT printer-id DEFINE-VALUE PRINTER_ID
    EXPECT printer-is-accepting-jobs IN-GROUP printer-attributes-tag OF-TYPE boolean COUNT 1
    EXPECT printer-state IN-GROUP printer-attributes-tag OF-TYPE enum COUNT 1
    EXPECT printer-state-reasons IN-GROUP printer-attributes-tag OF-TYPE keyword
    EXPECT printer-uuid IN-GROUP printer-attributes-tag OF-TYPE uri COUNT 1
    EXPECT printer-xri-supported IN-GROUP printer-attributes-tag OF-TYPE collection
    EXPECT printer-xri-supported/xri-uri OF-TYPE uri COUNT 1
    EXPECT printer-xri-supported/xri-uri DEFINE-VALUE PRINTER_URI
}

{
    NAME "Set-System-Attributes"
    OPERATION Set-System-Attributes
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
    GROUP system-attributes-tag
	ATTR integer system-default-printer-id $PRINTER_ID

    STATUS successful-ok
}

{
    NAME "Delete-Printer (Test Printer)"
    OPERATION Delete-Printer
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR integer printer-id $PRINTER_ID

    STATUS successful-ok
}

{
    NAME "Check for Register-Output-Device"
    OPERATION Get-System-Attributes
    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri system-uri $uri
    ATTR keyword requested-attributes operations-supported

    STATUS successful-ok

    EXPECT operations-supported IN-GROUP system-attributes-tag OF-TYPE enum
    EXPECT operations-supported WITH-VALUE 0x005F DEFINE-MATCH HAVE_REGISTER
}
{
    SKIP-IF-NOT-DEFINED HAVE_REGISTER

    NAME "Register-Output-Device (Cloud Printer)"
    OPERATION Register-Output-Device
    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri system-uri $uri
	ATTR uri output-device-uuid urn:uuid:abcd1234-abcd-1234-5678-0123456789ab
	ATTR keyword printer-service-type print

    STATUS successful-ok
    EXPECT printer-id IN-GROUP printer-attributes-tag OF-TYPE integer(1:MAX) COUNT 1
    EXPECT printer-id DEFINE-VALUE CLOUD_ID
    EXPECT printer-is-accepting-jobs IN-GROUP printer-attributes-tag OF-TYPE boolean COUNT 1
    EXPECT printer-state IN-GROUP printer-attributes-tag OF-TYPE enum COUNT 1
    EXPECT printer-state-reasons IN-GROUP printer-attributes-tag OF-TYPE keyword
    EXPECT printer-uuid IN-GROUP printer-attributes-tag OF-TYPE uri COUNT 1
    EXPECT printer-xri-supported IN-GROUP printer-attributes-tag OF-TYPE collection
    EXPECT printer-xri-supported/xri-uri OF-TYPE uri COUNT 1
}
